SKIP = /^#.*\n[^\S\n]*|^[^\S\n]+/
NEWL = /^\n+/
STR = /^"([^\\"]*(?:\\[\s\S][^\\"]*)*)"/
GROUP = /^\[([\w\d][\w\d\.]*)\]/
NUM = /^-?\d+(\.\d+)?/
BOOL = /^true|false/
DATE = // ^
  \d{4} -
  (0\d|1[012]) -
  ([012]\d|3[01]) T
  ([01]\d|2[01234]) :
  ([012345]\d) :
  ([012345]\d) Z
//
ID = /^\w[\w\d]+/
ARR_OPEN  = /^\[/
ARR_CLOSE = /^\]/
ASSIGN = 0x3d
COMMA = 0x2c

function err msg, tok => throw Error "#msg at line #{tok.line}"

function is-value tok => tok.type of <[ string datetime number boolean ]>

function set ctx, key, val
  key /= \.
  fkey = key.pop!
  for key => ctx = ctx@[&]
  ctx[fkey] = val

function lex str
  str += \\n; stack = [] <<< in-arr: 0 line: 0
  tok = -> stack.push @@{ type: 0 value: 1 } <<< stack{line}
  for ever
    l = 0
    switch
    case SKIP.exec      str => l=that.0.length
    case m = NEWL.exec  str =>
      stack.line += m.0.length
      tok \newline m.0 if stack.in-arr < 1
      l=m.0.length
    case STR.exec       str => tok \string   that.1;            l=that.0.length
    case DATE.exec      str => tok \datetime new Date   that.0; l=that.0.length
    case NUM.exec       str => tok \number   parseFloat that.0; l=that.0.length
    case BOOL.exec      str => tok \boolean  { true, false }[that.0]; l=that.0.length
    case ID.exec        str => tok \id       that.0;            l=that.0.length
    case GROUP.exec     str => tok \group    that.1;            l=that.0.length
    case ARR_OPEN.exec  str => stack.in-arr++; tok \arr_open \[;  l=1
    case ARR_CLOSE.exec str => stack.in-arr--; tok \arr_close \]; l=1
    case str.char-code-at! is ASSIGN => tok \assign \=;           l=1
    case str.char-code-at! is COMMA  => tok \comma  \,;           l=1
    default => err "Unexpected #{str.char-at!}"

    break unless str.=slice l
  stack

function parse-array stack
  arr = []
  type = void
  expects = true
  while tok = stack.shift!
    if tok.type is \arr_open
      arr.push parse-array stack
      expects = false
    else if tok.type is \arr_close
      return arr
    else if tok.type is \comma and not expects
      expects = true
    else if is-value tok and expects
      {type} ?= tok
      if tok.type is type
      then arr.push tok.value; expects = false
      else err "Inconsistent types in array" tok
    else err "Unexpected #{tok.type}" tok

function parse stack
  stack = lex stack if typeof stack is \string
  obj = {}
  key = group = ''
  while tok = stack.shift!
    switch tok.type
    case \group =>
      # detect dupe prop
      sub = obj; parts = tok.value / '.'
      for parts
        switch typeof sub[&]
        case \object => sub.=[&]
        case \undefined => break
        default => err "Duplicate property `#group#&`" tok
      group = tok.value + \.
    case \id =>
      if last is \newline
      then key = (group or '') + tok.value
      else err "Unexpected key" tok
    case \assign => err "Unexpected assign" tok unless last is \id
    case \arr_open =>
      if last is \assign
      then set obj, key, parse-array stack
      else err "Unexpected value" tok
    case \string \number \boolean \datetime =>
      if last is \assign
      then set obj, key, tok.value
      else err "Unexpected value" tok
    case \newline =>
      err "Expected value" tok if last is \assign
    default => err "Unexpected `#{if tok.value is \n then \newline else tok.value}`" tok
    last = that.type
  obj

export lex, parse
